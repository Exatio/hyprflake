{ pkgs, lib, ... }:
let
  pywalfox-wrapper = pkgs.writeShellScriptBin "pywalfox-wrapper" ''
    ${pkgs.pywalfox-native}/bin/pywalfox start
  '';
in
{
  home.packages = with pkgs; [
    matugen
    pywalfox-native
    libnotify
  ];

  services.dunst = {
    enable = true;
    package = pkgs.dunst.overrideAttrs {
      patches = [
        (pkgs.fetchpatch {
          # patch to fix `dunstctl reload` on native wayland dunst
          name = "1458.patch";
          url = "https://github.com/dunst-project/dunst/pull/1458.patch";
          sha256 = "sha256-uLY0atUjHRy7hCkAoEkWRk5kl8VvO6nygwuK5aqaG5c=";
        })
      ];
    };

    configFile = "~/.config/dunst/dunstrc_mutable"; # we need dunst to load a mutable file so that we can append the config generated by nix to the config generated by mutagen
  };

  home.file.".config/matugen/config.toml".source = ./config.toml;

  home.file.".mozilla/native-messaging-hosts/pywalfox.json".text = lib.replaceStrings [ "<path>" ] [
    "${pywalfox-wrapper}/bin/pywalfox-wrapper"
  ] (lib.readFile "${pkgs.pywalfox-native}/lib/python3.13/site-packages/pywalfox/assets/manifest.json");

}
